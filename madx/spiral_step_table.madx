!-----------------------------------------------------------------------
! Nominal setup for SPS slow extraction (at resonance)
!
! F.M. Velotti - francesco.maria.velotti@cern.ch
! L.S. Stoel - linda.susanne.stoel@cern.ch
!
!-----------------------------------------------------------------------

!-----------------------------------------------------------------------
! Initialize nominal optics and bump macro
! Will set OPTION, INFO, WARN, -ECHO, RBARC=FALSE;
!-----------------------------------------------------------------------

CALL, FILE = "../init/nominal.ini" ;
CALL, FILE = "../cmd/setup_nominal.cmdx";

SELECT, FLAG = TWISS, CLEAR;
SELECT, FLAG = TWISS, COLUMN = NAME,S,L,BETX,ALFX,MUX,X,PX,DX,DPX,
        APER_1,BETY,ALFY,MUY,Y,PY,DY,DPY,APER_2,K2L;


!-----------------------------------------------------------------------
! Cycle SPS to start at the ZS entrance
!-----------------------------------------------------------------------

SEQEDIT, SEQUENCE = sps;
FLATTEN;
CYCLE, START = AP.UP.ZS21633;
FLATTEN;
ENDEDIT;
USE, SEQUENCE = sps;
EXEC, activate_bump(knob_extr_bump);


!-----------------------------------------------------------------------
! Create table and macro for spiral step calculations
!-----------------------------------------------------------------------

CREATE, TABLE = spiral_step, COLUMN = knob_value, delta_x, delta_px,
        x_zs, px_zs,  final_x, final_px;

get_spiral_step(x0_bump, px0_bump): MACRO = {
 m = -0.02025305; ! TODO: Slope of separatrix in phase space, magic?

 x_zs = 68e-3;
 px_zs = m * (x_zs - x0_bump) + px0_bump;

 zs_start2->x = x_zs;
 zs_start2->px = px_zs;

 SAVEBETA, LABEL = turn1, PLACE = SPS2AP.UP.ZS21633_P_;
 TWISS, DELTAP = 0.0, beta0 = zs_start2;

 SAVEBETA, LABEL = turn2, PLACE = SPS2AP.UP.ZS21633_P_;
 TWISS, DELTAP = 0.0, beta0 = turn1;

 TWISS, DELTAP = 0.0, beta0 = turn2;

 delta_x = TABLE(TWISS, SPS2AP.UP.ZS21633_P_, X) - x_zs;
 delta_px = TABLE(TWISS, SPS2AP.UP.ZS21633_P_, PX) - px_zs;

 final_x = TABLE(TWISS, SPS2AP.UP.ZS21633_P_, X);
 final_px = TABLE(TWISS, SPS2AP.UP.ZS21633_P_, PX);
};


!-----------------------------------------------------------------------
! Calculate spiral step for various bump settings
!-----------------------------------------------------------------------
tablen = 40
bumpstart = 0.8
bumpend = 1.2

i = 0;
WHILE(i < tablen){
 knob_inc = (bumpend - bumpstart)/(tablen - 1);
 knob_value = bumpstart + i * knob_inc;

 USE, SEQUENCE = sps;
 EXEC, activate_bump(knob_value);
! TODO: Rematch tune+chroma?

 TWISS; !
 x0 = TABLE(TWISS, AP.UP.ZS21633, X);
 px0 = TABLE(TWISS, AP.UP.ZS21633, PX);
     
 EXEC, get_spiral_step(x0, px0);
 FILL, TABLE = spiral_step;
 i = i + 1;
};

WRITE, TABLE = spiral_step, FILE = "../out/spiral_step.tfs";

RETURN;
